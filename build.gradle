def getVersion = { ->
    def p = 'git describe --tags'.execute(); p.waitFor()
    def version = p.exitValue() == 0 ? p.text.trim() : '0.0.0'
    def dirty = 'git status -s'.execute().text.trim()
    return dirty ? "${version}-SNAPSHOT" : version
}

group 'com.github.wfxr'
version getVersion()

repositories {
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile group: 'net.sf.trove4j', name: 'trove4j', version: '3.0.3'

    testCompile "org.slf4j:slf4j-api:1.6.1"
    testRuntime "org.slf4j:slf4j-log4j12:1.6.1"
    testRuntime "log4j:log4j:1.2.16"
    testCompile "junit:junit:4.11"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10'
}

test {
    testLogging {
        exceptionFormat "short"
        events "failed", "started"
    }
    filter {
        includeTestsMatching "*Test"
    }
}
